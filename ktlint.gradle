// kotlin-gradle-plugin must be applied for configuration below to work
// (see https://kotlinlang.org/docs/reference/using-gradle.html)

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.31.0"

    // Additional 3rd party ruleset(s) can be specified here
    // ktlint 'groupId:artifactId:version'
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style"
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"

    // Use checkstyle prefix and reporting style FBO Jenkins checkstyle plugin
    def ktlintReport = "${buildDir}/reports/checkstyle-ktlint.xml"

    // https://github.com/shyiko/ktlint#usage
    args "--android",
        "--reporter=plain",
        "--reporter=checkstyle,output=${ktlintReport}",
        "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFix(type: JavaExec, group: "formatting") {
    description = "Fix (some) Kotlin code style deviations"
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F",
        "--android",
        "--reporter=plain",
        "src/**/*.kt"
}
