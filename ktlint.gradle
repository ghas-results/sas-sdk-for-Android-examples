// kotlin-gradle-plugin must be applied for configuration below to work
// (see https://kotlinlang.org/docs/reference/using-gradle.html)

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.43.2"

    // Additional 3rd party ruleset(s) can be specified here
    // ktlint 'groupId:artifactId:version'
}

def disabledRules = [
    // Adds a blank line after multiple annotations; unnecessary
    "experimental:annotation-spacing",

    // One line per method argument; unnecessary
    "experimental:argument-list-wrapping",

    // Places braces around multi-line if-else blocks; unnecessary
    "experimental:multiline-if-else",

    // XXX Re-enable when this is integrated:
    // https://github.com/pinterest/ktlint/issues/1280
    "experimental:trailing-comma",
]

def commonArgs = [
    "--android",
    "--reporter=plain",
    "--experimental",
    "--disabled_rules=${disabledRules.join(",")}",
    "src/**/*.kt"
]

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style"
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"

    // Use checkstyle prefix and reporting style FBO Jenkins checkstyle plugin
    def ktlintReport = "${buildDir}/reports/checkstyle-ktlint.xml"

    // https://github.com/shyiko/ktlint#usage
    args commonArgs +
        "--reporter=checkstyle,output=${ktlintReport}"
}
check.dependsOn ktlint

task ktlintFix(type: JavaExec, group: "formatting") {
    description = "Fix (some) Kotlin code style deviations"
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args commonArgs +
        "-F"
}
